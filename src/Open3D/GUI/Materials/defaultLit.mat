material {
    name : defaultLit,
    shadingModel : lit,
    doubleSided : true,

    parameters : [
        { type : float3,    name : baseColor },
        { type : float,     name : baseMetallic },
        { type : float,     name : baseRoughness },
        { type : float,     name : reflectance },
        { type : float,     name : clearCoat },
        { type : float,     name : clearCoatRoughness },
        { type : float,     name : anisotropy },
        { type : float,     name : pointSize },
        { type : sampler2d, name : albedo },
        { type : sampler2d, name : roughMetalReflectAOMap },
        { type : sampler2d, name : normalMap },
        { type : sampler2d, name : clearCoatCCRoughAnisoMap }
    ],
    requires : [
        color, uv0
    ]
}

vertex {
    void materialVertex(inout MaterialVertexInputs material) {
        gl_PointSize = materialParams.pointSize;
    }
}

fragment {
    void material(inout MaterialInputs material) {
        // Modifications to normal must be done before prepareMaterial call
        material.normal = texture(materialParams_normalMap, getUV0()).xyz * 2.0 - 1.0;
        material.normal.y *= -1.0; // NOTE: including this line because Filament samples do

        prepareMaterial(material);

        material.baseColor.rgb = getColor().rgb*materialParams.baseColor * texture(materialParams_albedo, getUV0()).rgb;
        material.roughness = materialParams.baseRoughness * texture(materialParams_roughMetalReflectAOMap, getUV0()).r;
        material.metallic = materialParams.baseMetallic * texture(materialParams_roughMetalReflectAOMap, getUV0()).g;
        material.reflectance = materialParams.reflectance * texture(materialParams_roughMetalReflectAOMap, getUV0()).b;
        material.ambientOcclusion = texture(materialParams_roughMetalReflectAOMap, getUV0()).a;
        material.clearCoat = materialParams.clearCoat * texture(materialParams_clearCoatCCRoughAnisoMap, getUV0()).r;
        material.clearCoatRoughness = materialParams.clearCoatRoughness * texture(materialParams_clearCoatCCRoughAnisoMap, getUV0()).g;
        material.anisotropy = materialParams.anisotropy * texture(materialParams_clearCoatCCRoughAnisoMap, getUV0()).b;
    }
}
